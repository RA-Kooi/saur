# Maintainer: Sam Mulvey <archlinux@sammulvey.com>
pkgname=xen-ovmf
pkgver=202403
pkgrel=1
pkgdesc="a version of edk2-ovmf maintained by the Xen Project for use with that hypervisor"
arch=("x86_64")
url="https://github.com/tianocore/edk2"
license=(
  Apache-2.0
  BSD-2-Clause-Patent
  MIT
)

makedepends=('git' 'nasm')
source=(
	Makefile
	'git+https://xenbits.xen.org/git-http/ovmf.git'
	'git+https://github.com/openssl/openssl'
	'git+https://github.com/ucb-bar/berkeley-softfloat-3.git'
	'git+https://github.com/tianocore/edk2-cmocka.git'
	'git+https://github.com/kkos/oniguruma'
	'git+https://github.com/google/brotli'
	'git+https://github.com/akheron/jansson'
	'git+https://github.com/google/googletest.git'
	'git+https://github.com/Zeex/subhook.git'
	'git+https://github.com/devicetree-org/pylibfdt.git'
	'git+https://github.com/MIPI-Alliance/public-mipi-sys-t.git'
)
sha256sums=('53f8b4499acbfd7fe14fdab67df321faabcb97c2964ee5ac90d01e1434942224'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')



prepare() {
	cd "${srcdir}/ovmf"
	cp "${srcdir}/Makefile" .
	git config submodule.CryptoPkg/Library/OpensslLib/openssl.url                  "${srcdir}/openssl"
	git config submodule.SoftFloat.url                                             "${srcdir}/berkeley-softfloat-3"
	git config submodule.UnitTestFrameworkPkg/Library/CmockaLib/cmocka.url         "${srcdir}/edk2-cmocka"
	git config submodule.MdeModulePkg/Universal/RegularExpressionDxe/oniguruma.url "${srcdir}/oniguruma"
	git config submodule.MdeModulePkg/Library/BrotliCustomDecompressLib/brotli.url "${srcdir}/brotli"
	git config submodule.BaseTools/Source/C/BrotliCompress/brotli.url              "${srcdir}/brotli"
	git config submodule.RedfishPkg/Library/JsonLib/jansson.url                    "${srcdir}/jansson"
	git config submodule.UnitTestFrameworkPkg/Library/GoogleTestLib/googletest.url "${srcdir}/googletest"
	git config submodule.UnitTestFrameworkPkg/Library/SubhookLib/subhook.url       "${srcdir}/subhook"
	git config submodule.MdePkg/Library/BaseFdtLib/libfdt.url                      "${srcdir}/pylibfdt"
	git config submodule.MdePkg/Library/MipiSysTLib/mipisyst.url                   "${srcdir}/public-mipi-sys-t"
	git -c protocol.file.allow=always submodule update --init --recursive
	
}

build() {
	cd "${srcdir}/ovmf"
	make
}

package() {
	mkdir -p "${pkgdir}/usr/share/xen-ovmf"
	install -m644 "${srcdir}/ovmf/ovmf.bin" "${pkgdir}/usr/share/xen-ovmf"
}
