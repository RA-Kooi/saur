# Maintainer: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: hm_b <holger@music-nerds.net>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=jack_capture-refutationalist
_pkgname=jack_capture
provides=($_pkgname)
conflicts=($_pkgname)
pkgver=r161.2e6cc9e
pkgrel=1
pkgdesc="Simple command line tool to record JACK audio output to a file"
arch=('i686' 'x86_64')
url="http://www.notam02.no/arkiv/src/"
license=('GPL')
depends=('jack' 'lame' 'liblo')
optdepends=('meterbridge: levels monitor'
            'zenity: gtk2 gui')
makedepends=('git')
source=("${_pkgname}::git+https://github.com/refutationalist/jack_capture.git") 
md5sums=('SKIP')



pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"

  make
}

package(){
  cd "$srcdir/$_pkgname"

  make PREFIX="$pkgdir/usr" install

  # gtk2 gui
  install -Dm755 ${_pkgname}_gui "$pkgdir/usr/bin/${_pkgname}_gui"

  # doc
  install -Dm644 README "$pkgdir/usr/share/doc/$_pkgname/README"
}

