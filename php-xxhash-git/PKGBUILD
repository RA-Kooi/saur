# Maintainer: Sam Mulvey <archlinux@sammulvey.com>
# shamelessly stolen from php-lzo-git

_pkgname=php-xxhash
pkgname=${_pkgname}-git
pkgver=r15.df03d35
pkgrel=1
pkgdesc="xxhash fast non-crypto hashing for php"
arch=('i686' 'x86_64')
license=('PHP')
url='https://github.com/adsr/php-xxhash'
depends=('php')
makedepends=('git')
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("git+https://github.com/refutationalist/php-xxhash")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"

  phpize
  ./configure
  make
}

package() {
  cd "${srcdir}/${_pkgname}"

  make install INSTALL_ROOT="${pkgdir}"
  echo ';extension=xxhash.so' >xxhash.ini
  install -Dm644 xxhash.ini "$pkgdir/etc/php/conf.d/xxhash.ini"
}

# vim:set ts=2 sw=2 et:
