_pkgname=merecat
pkgname=merecat-sam
pkgver=20190422.d4e548b
pkgrel=1
pkgdesc="simple HTTP/HTTPS server based on Jef Poskanzer's thttpd (with PHP modifications)"
url='https://github.com/refutationalist/merecat'
license=('custom:BSD')
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
backup=('etc/merecat/merecat.conf' 'etc/merecat/merecats.conf' 'etc/merecat/throttle.conf')
makedepends=('git' 'confuse' 'openssl' 'gzip')
source=(
	'git+https://github.com/refutationalist/merecat'
	'merecat.diff'
)
sha1sums=('SKIP'
          '4420ad3a369fd2bf1469b418845b624f7b477c31')


provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	patch -p1 < ${srcdir}/merecat.diff
	./autogen.sh
}

build() {
	cd "${srcdir}/${_pkgname}"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man \
		--sysconfdir=/etc/merecat \
		--enable-htaccess \
		--enable-htpasswd \
		--enable-builtin-icons \
		--datarootdir=/srv/http \
		--without-symlinks

	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ${srcdir}/merecat/throttle.conf ${pkgdir}/etc/merecat/throttle.conf

	# Avoid conflicts with Apache
	mv "${pkgdir}"/usr/bin/htpasswd{,-merecat}
	mv "${pkgdir}"/usr/share/man/man1/htpasswd{,-merecat}.1
}
