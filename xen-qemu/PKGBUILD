# Maintainer: Refutationalist <archlinux@sammulvey.com>
pkgname=xen-qemu
_srcname=qemu
pkgver=8.2.1
pkgrel=1
pkgdesc="A xen-specific QEMU, built to qemu-builtin standards"
arch=(x86_64)
url="https://www.qemu.org"
license=(GPL2 LGPL2.1)

depends=(
	'curl'
	'xen'
	'numactl'
	'dtc'
	'fuse3'
	'gcc-libs'
	'glibc'
	'glib2' 'libgio-2.0.so' 'libglib-2.0.so' 'libgmodule-2.0.so'
	'gnutls'
	'keyutils'
	'libaio'
	'libelf'
	'libbpf' 'libbpf.so'
	'libjpeg-turbo' 'libjpeg.so'
	'libpng'
	'libsasl'
	'libseccomp' 'libseccomp.so'
	'libslirp' 'libslirp.so'
	'liburing' 'liburing.so'
	'libxdp' 'libxdp.so'
	'lzo'
	'ndctl'
	'numactl' 'libnuma.so'
	'pam' 'libpam.so'
	'pixman' 'libpixman-1.so'
	'qemu-common='$pkgver'-'$pkgrel
	'snappy'
	'vde2'
	'zlib'
	'zstd' 'libzstd.so'
)

makedepends=(
	'ninja'
	'brltty'
	'bzip2'
	'dtc'
	'fuse3'
	'gcc-libs'
	'glib2'
	'gnutls'
	'keyutils'
	'libaio'
	'libbpf'
	'libcap-ng'
	'libpng'
	'libsasl'
	'libseccomp'
	'libslirp'
	'liburing'
	'libxdp'
	'libxml2'
	'libxkbcommon'
	'lzo'
	'meson'
	'ndctl'
	'numactl'
	'pam'
	'pcre2'
	'python'
	'python-distlib'
	'python-setuptools'
	'python-pip'
	'snappy'
	'vde2'
	'zlib'
	'zstd'
)

provides=('qemu-xen' 'xen-qemu' 'xen-qemu-builtin')
conflicts=('xen-qemu-builtin')
replaces=('xen-qemu-builtin')

_source=(
	https://download.qemu.org/qemu-${pkgver}.tar.xz{,.sig}
)

_sha512sums=(
	'e72d3e13339c03e8d371ca060ac700c45af2ca37523cddb6b02dcaf8430d75c8cef194cf496df9816440b281f368457def1126677db757928805d93ceca2f9af'
	'SKIP')

_patches=(
	"0005-hw-xen-xen_pt-Save-back-data-only-for-declared-regis.patch"
	"0006-Do-not-access-dev-mem-in-MSI-X-PCI-passthrough-on-Xe.patch"
	"0008-xen-round-pci-region-sizes.patch"
	"0011-Additional-seccomp-filters.patch"
	"0012-Register-ich6-ich9-soundhw.patch"
)

_patch_sums=(
	"e4f0ac29c7cc9d8e666303265c21834320379fc8617bd37f9f665f1cc88590ba87ab1361c1937e08e50fbdbc4d78b2cd40e988324fc978d964cfd69732fffc5e" #0005-hw-xen-xen_pt-Save-back-data-only-for-declared-regis.patch
	"fe28601746ab406e23970ec175fa4c657f5551719e74f5793b63fb7a919411afbcf66892018b57084d3d882fc297fd2d29d7fc721d2863f33938544e0f9e0643" #0006-Do-not-access-dev-mem-in-MSI-X-PCI-passthrough-on-Xe.patch
	"0c69f8edacc5b88de1215e20b99690a59039d5912ca17514059f422901eed87a714e9146ca2c13c785df8e077a6f2b7c24a19b4b60d7e481f8005d5e3120eea2" #0008-xen-round-pci-region-sizes.patch
	"f77a58b9d34dfb9d16b3ebe82db11791c6bfcebfa8e2caa77684420610ad8faf46499961b4f33a199aae006d9a29152fb0e2517635949e657e36bc784244c0d5" #0011-Additional-seccomp-filters.patch
	"74e02525d13f18fe8e8dbb6470794b7e0d133b18230d1d64dbaef93f2074ebda33263d1c5a0ac5752f221918aa143b50bdec3bde0ea24222962d7193e9c180db" #0012-Register-ich6-ich9-soundhw.patch
)

source=( "${_source[@]}" "${_patches[@]}" )
sha512sums=( "${_sha512sums[@]}" "${_patch_sums[@]}" )
noextract+=( "${_patches[@]##*/}" )

validpgpkeys=('CEACC9E15534EBABB82D3FA03353C9CEF108B584') # Michael Roth <flukshun@gmail.com>

prepare() {
	cd "${_srcname}-${pkgver}"

	for patchurl in "${_patches[@]}"; do
		patch=$(basename $patchurl)
		echo "==> Applying patch '${patch}'..."
		patch -p1 < "../${patch}"
	done
}

build() {
	cd $srcdir
	mkdir -p build
	cd build

	"${srcdir}/${_srcname}-${pkgver}/configure" \
		--enable-xen \
		--enable-xen-pci-passthrough \
		--target-list=i386-softmmu,x86_64-softmmu \
		--enable-trace-backends=log \
		--prefix=/usr \
		--extra-cflags="-DXC_WANT_COMPAT_EVTCHN_API=1  -DXC_WANT_COMPAT_GNTTAB_API=1 -DXC_WANT_COMPAT_MAP_FOREIGN_API=1 -DXC_WANT_COMPAT_DEVICEMODEL_API=1" \
		--localstatedir=/var \
		--libexecdir=/usr/lib/qemu \
		--smbd=/usr/bin/smbd \
		--with-coroutine=ucontext \
		--python=python3 \
		--cpu=x86_64 \
		--enable-af-xdp \
		--enable-auth-pam \
		--enable-bpf \
		--enable-bzip2 \
		--enable-cap-ng \
		--enable-crypto-afalg \
		--enable-fuse\
		--enable-gio \
		--enable-gnutls \
		--enable-libdaxctl \
		--enable-libdw \
		--enable-libkeyutils \
		--enable-libudev \
		--enable-linux-aio \
		--enable-linux-io-uring \
		--enable-lzo \
		--enable-modules \
		--enable-nettle \
		--enable-numa \
		--enable-pixman \
		--enable-png \
		--enable-seccomp \
		--enable-slirp \
		--enable-snappy \
		--enable-vde \
		--enable-vnc \
		--enable-vnc-jpeg \
		--enable-vnc-sasl \
		--enable-zstd \
		--disable-alsa \
		--disable-bsd-user \
		--disable-capstone \
		--disable-curl \
		--disable-curses \
		--disable-dmg \
		--disable-docs \
		--disable-gcrypt \
		--disable-glusterfs \
		--disable-gtk \
		--disable-guest-agent \
		--disable-install-blobs \
		--disable-kvm \
		--disable-libiscsi \
		--disable-libnfs \
		--disable-libssh \
		--disable-libusb \
		--disable-linux-user \
		--disable-opengl \
		--disable-oss \
		--disable-qcow1 \
		--disable-qom-cast-debug \
		--disable-sdl \
		--disable-tcg \
		--disable-tools \
		--disable-tpm \
		--disable-vhost-crypto \
		--disable-vhost-kernel \
		--disable-vhost-net \
		--disable-vhost-user \
		--disable-vhost-vdpa \
		--disable-werror

	ninja
}

package() {
	cd "${srcdir}/build"
	# meson direct was failing me, but this worked.
	# might be a backwards compatible thing
	make DESTDIR="$pkgdir/" install

	cd "${pkgdir}/usr"
	rm -r lib share

	cd bin
	mv qemu-system-i386 qemu-system-i386-xen
	mv qemu-system-x86_64 qemu-system-x86_64-xen
}
