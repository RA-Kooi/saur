# Maintainer: Sam Mulvey <archlinux@sammulvey.com>
pkgname=libwebsock-git
_pkgname=libwebsock
pkgver=1.0.7.r20.6912715
_pkgver=1.0.7
pkgrel=1
pkgdesc="a simple websocket library in C"
arch=("x86_64" "aarch64")
url="https://github.com/JonnyWhatshisface/libwebsock"
license=('LGPL3')
depends=('libevent')
provides=('libwebsock')
source=("git://github.com/JonnyWhatshisface/libwebsock")
md5sums=('SKIP')


pkgver() {
	cd "$_pkgname"
	printf "%s.r%s.%s" "$_pkgver" "$(git rev-list --count HEAD)"  "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$_pkgname"
	./autogen.sh
}

build() {
	cd "$_pkgname"
	./configure --prefix=/usr
	make
}

check() {
	cd "$_pkgname"
	make -k check
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir/" install
}
