# If you happen upon this:

# I need to be able to run Audacity 3.0 sooner rather than later as I
# train people in it and the people I train usually run Windows or MacOS.
# I would also like to have the Arch Linux version of Audacity available
# to me for stability reasons.  This is why this package exists.

# That aside, I support the effort and the time the Arch Linux team is 
# spending to make Audacity easier to build (and build repeatably) in a
# proper manner.  See <https://github.com/audacity/audacity/issues/625>
# for an example.

# As a result, I'm not advertising this pacakge, I'm not supporting it, 
# and the profanity is designed to scare you off.


# GREETZ: osch, audacity-git folks on AUR

pkgname=audacity-bs
_projname=audacity
_location=/opt/audacity-bs
pkgver=3.1.0
pkgrel=1
# This is kind of intentional.  I try not to be this much of a jackass.
pkgdesc="UNSUPPORTED bullshit audacity until tenacity is ready"
arch=('x86_64')
url="https://audacityteam.org"
license=('GPL2' 'CCPL')
groups=('pro-audio')
# gtk3 -> gtk2 until it's fixed
depends=(
  flac
  gtk2
  libid3tag
  libmad
  libvorbis
  lilv
  lv2
  portaudio
  portsmf
  soundtouch
  suil
  twolame
  vamp-plugin-sdk
  ffmpeg
)
makedepends=(
  cmake
  conan
  gcc
  git
  gst-plugins-bad-libs
  gstreamer
  jack
  libnotify
  libsoup
  nasm
  sdl2
)
provides=("audacity")
source=("https://github.com/audacity/audacity/archive/Audacity-${pkgver}.tar.gz" "audacity-bs")
sha512sums=('ddaad6af31fc96357fdb3f1f8b5e8039c4158545e472bcac01440ae0b0ed54d7839b84b756b13f23c013bac93da8753064194452e4d29fd73a74ec36f1394d88'
            'd1704ad61a62a63b9925a82fed506d9a7cba4b44eaaf4318ac196c1527416fb90a35934c70e2a030c322df6d3332a6f8081818d232116e2a9bf45185e8ac85c9')


prepare() {
	mv "audacity-Audacity-${pkgver}" "${_projname}"
	mkdir {conan,build}
}

build() {
	cd "${srcdir}/build"

	# Calling cmake compiles stuff now because conan.

	CC=gcc CONAN_USER_HOME="${srcdir}/conan" cmake \
		-DCMAKE_INSTALL_PREFIX="${_location}" \
		-DCMAKE_BUILD_TYPE=Release \
		-DAUDACITY_BUILD_LEVEL=2 \
		-Daudacity_use_wxwidgets=local \
		-Daudacity_conan_allow_prebuilt_binaries=Off \
		-Daudacity_use_ffmpeg:STRING=loaded \
        -Daudacity_lib_preference=system \
        -Daudacity_obey_system_dependencies=On \
		-Daudacity_has_networking=Off \
		-Daudacity_use_sbsms=local \
		"../${_projname}"

	# THINGS WHAT ARE BROKE
	#-Daudacity_use_ffmpeg=local \
	#-Daudacity_use_wxwidgets=local \
	#-Daudacity_use_sqlite=local \
	#-DwxBUILD_TOOLKIT=gtk3 \

	CONAN_USER_HOME="${srcdir}/conan" cmake --build .
}

package() {
	mkdir -p \
		"${pkgdir}/usr/bin" \
		"${pkgdir}/usr/share/applications"



	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install

	install -m755 "${srcdir}/audacity-bs" "${pkgdir}/usr/bin"

	cp "${pkgdir}/opt/audacity-bs/share/applications/audacity.desktop" "${pkgdir}/usr/share/applications/audacity-bs.desktop"
	sed -i 's#Audacity#Audacity-BS#' "${pkgdir}/usr/share/applications/audacity-bs.desktop"
	sed -i 's#UBUNTU_MENUPROXY=0 audacity#GDK_BACKEND=x11 UBUNTU_MENUPROXY=0 /opt/audacity-bs/bin/audacity#' "${pkgdir}/usr/share/applications/audacity-bs.desktop"
}
